sdnl
dnl Require autoconf version 2.13
dnl
AC_PREREQ(2.13)

AC_INIT(src/dvdnav.h)

DVDNAV_MAJOR=0
DVDNAV_MINOR=1
DVDNAV_SUB=1
DVDNAV_PRE=""
PACKAGE="libdvdnav"
RELEASE="1"
TAR_NAME=$PACKAGE-$DVDNAV_MAJOR.$DVDNAV_MINOR.$DVDNAV_SUB$DVDNAV_PRE
SPEC_VERSION=$DVDNAV_MAJOR.$DVDNAV_MINOR.$DVDNAV_SUB$DVDNAV_PRE
AC_SUBST(DVDNAV_MAJOR)
AC_SUBST(DVDNAV_MINOR)
AC_SUBST(DVDNAV_SUB)
AC_SUBST(TAR_NAME)
AC_SUBST(SPEC_VERSION)
AC_SUBST(PACKAGE)
AC_SUBST(RELEASE)

dnl this is only for pre-1.0 releases (ie with unstable interface)
LT_RELEASE=$DVDNAV_MAJOR.$DVDNAV_MINOR.$DVDNAV_SUB
LT_CURRENT=0
LT_REVISION=0
LT_AGE=0

dnl non-devel releases should not use LT_RELEASE but something like this:
dnl
dnl LT_REVISION=$XINE_SUB
dnl LT_CURRENT=`expr $XINE_MAJOR + $XINE_MINOR`
dnl LT_AGE=$XINE_MINOR
dnl
dnl !! This only works if subminor releases don't have any interface changes,
dnl !! minor releases _only add_ interfaces and major releases remove at least
dnl !! one interface
dnl otherwise, the LT_* variables must be set according to
dnl <info:(libtool)Updating version info>

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl Every other copy of the package version number gets its value from here
AM_INIT_AUTOMAKE("libdvdnav", $DVDNAV_MAJOR.$DVDNAV_MINOR.$DVDNAV_SUB$DVDNAV_PRE)

dnl
dnl Made possible to build for another arch.
dnl
if test x$DVDNAV_BUILD != "x"; then
  AC_MSG_RESULT(*** build forced to $DVDNAV_BUILD ***)
  build=$DVDNAV_BUILD
  host=$DVDNAV_BUILD
else
  check_athlon=yes
fi

dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AM_CONFIG_HEADER(config.h)

AC_SUBST(VERSION)

ISODATE=`date +%Y-%m-%d`
AC_SUBST(ISODATE)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_LN_S


dnl
dnl Libtool
dnl
AC_LIBTOOL_DLOPEN
AM_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
if ${CONFIG_SHELL} ./libtool --features | grep "enable static" >/dev/null; then
  STATIC="-static"
else
  STATIC=
fi
AC_SUBST(STATIC)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)

dnl
dnl Checks for typedefs, structures, and compiler characteristics.
dnl
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AM_TYPE_PTRDIFF_T
AC_C_BIGENDIAN

dnl
dnl define or no in dvdnav_internal.h WORDS_BIGENDIAN
dnl
if test x$ac_cv_c_bigendian = "xyes"; then
  BIGENDIAN="#define WORDS_BIGENDIAN 1"
else
  BIGENDIAN="#undef WORDS_BIGENDIAN"
fi
AC_SUBST(BIGENDIAN)

AC_SUBST(DEBUG_CFLAGS)
AC_SUBST(GLOBAL_CFLAGS)
dnl AC_SUBST(ROOT_CFLAGS)
dnl AC_SUBST(TOOLS_CFLAGS)

dnl
dnl threads
dnl

case $host in
  *-*-freebsd*)
    THREAD_LIBS="-pthread"
    THREAD_CFLAGS="$CFLAGS -I/usr/local/include -L/usr/local/lib -D_THREAD_SAFE"
    CPPFLAGS="$CPPFLAGS -I/usr/local/include -L/usr/local/lib"
    ;;
  *)
    AC_CHECK_LIB(pthread, pthread_create,
  	     THREAD_LIBS="-lpthread",
 	     AC_MSG_ERROR(pthread needed))
    ;;
esac
AC_SUBST(THREAD_LIBS)
AC_SUBST(THREAD_CFLAGS)

dnl
dnl dynamic linker
dnl
AC_CHECK_LIB(c, dlopen,
	     DYNAMIC_LD_LIBS="",
	     AC_CHECK_LIB(dl, dlopen,
             	          DYNAMIC_LD_LIBS="-ldl",
	                  AC_MSG_ERROR(dynamic linker needed)))
AC_SUBST(DYNAMIC_LD_LIBS)

dnl
dnl Check some header files
dnl
AC_CHECK_HEADER(string.h)

dnl 
dnl Check for DVDReader
dnl
AC_CHECK_LIB(dvdread,
             DVDOpen,
 	     [DVDREAD_LIBS="-ldvdread"
	     AC_DEFINE(HAVE_LIBDVDREAD)
 	     AC_CHECK_LIB(dvdread,
                          UDFReadLB,
                          [ AC_MSG_ERROR(You need at least libdvdread 0.9.x or higher)
                          ],
                          [ :
                          ])
	     ],
 	     AC_MSG_ERROR(Could not find libdvdread. It is needed))
AC_SUBST(DVDREAD_LIBS)

dnl
dnl *CFLAGS*
dnl

dnl Common cflags for all platforms
COMMON_CFLAGS="-Wall -D_FILE_OFFSET_BITS=64 -D_LARGEFILE64_SOURCE"
GLOBAL_CFLAGS="$GLOBAL_CFLAGS -O3 $COMMON_CFLAGS"
DEBUG_CFLAGS="$DEBUG_CFLAGS -O3 $COMMON_CFLAGS -g -DDEBUG"


dnl
dnl Get where .m4 should be installed.
dnl
case "`id`" in
  uid=0\(* )
    AC_MSG_CHECKING(for aclocal directory)
    if (aclocal --version) < /dev/null > /dev/null 2>&1; then
      ACLOCAL_DIR="`eval $ACLOCAL --print-ac-dir`"
      AC_MSG_RESULT($ACLOCAL_DIR)
    else
      ACLOCAL_DIR="/usr/local/share/aclocal"
      AC_MSG_RESULT(none - will be installed in $ACLOCAL_DIR)
    fi
    escapedprefix="`echo $prefix | sed -e 's/\\//\\\\\//g'`"
    ACLOCAL_DIR="`echo $ACLOCAL_DIR|sed -e 's/^'$escapedprefix/'\${prefix}'/`"
    AC_SUBST(ACLOCAL_DIR)
    ;;
esac
AM_CONDITIONAL(INSTALL_M4, test x"$ACLOCAL_DIR" != "x")


dnl
dnl Some include paths ( !!! DO NOT REMOVE !!! )
dnl
INCLUDES='-I$(top_srcdir) $(DVDNAV_CFLAGS)'
AC_SUBST(INCLUDES)


dnl
dnl It seems automake 1.5 don't take care about this script
dnl
if test ! -z $am_depcomp; then
	DEPCOMP="depcomp"
fi
AC_SUBST(DEPCOMP)

	
dnl
dnl Output configuration files
dnl
AC_OUTPUT([
Makefile 
src/Makefile
src/dvdnav_internal.h
examples/Makefile
misc/Makefile
misc/dvdnav-config
m4/Makefile
libdvdnav.spec
], [echo '/* !! DO NO EDIT THIS FILE, it is automatically generated */' > src/dvdnav_internal.h.tmp && cat src/dvdnav_internal.h >> src/dvdnav_internal.h.tmp && mv -f src/dvdnav_internal.h.tmp src/dvdnav_internal.h])
